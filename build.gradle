plugins {
    id 'java-library'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
}

checkstyle {
    toolVersion "10.2"
}

group = 'com.github.biczomate'
version = '1.3.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.eclipse.californium:californium-core:3.6.0'
    implementation 'org.eclipse.californium:scandium:3.6.0'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.2'
}

tasks.withType(JavaCompile).configureEach {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            pom {
                name = 'ikea-tradfri-api'
                description = 'A Java library for communicating with IKEA TRÅDFRI devices'
                url = 'https://github.com/biczomate/ikea-tradfri-api'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'biczomate'
                        name = 'Máté Biczó'
                        email = 'biczomate@hotmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/biczomate/ikea-tradfri-api.git'
                    developerConnection = 'scm:git:git://github.com/biczomate/ikea-tradfri-api.git'
                    url = 'https://github.com/biczomate/ikea-tradfri-api'
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}